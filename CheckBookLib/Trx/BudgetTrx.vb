Option Strict On
Option Explicit On

''' <summary>
''' A BaseTrx subclass representing an amount of money set aside for some use.
''' TrxSplit objects can reference a BudgetTrx, which causes the effective
''' amount of the budget item used in running balance computations to be
''' different than the nominal budget amount.
''' </summary>

Public Class BudgetTrx
    Inherits BaseTrx

    'Original budget amount.
    'Is NOT changed as other TrxSplit are applied and unapplied to this one.
    'Sign has the same meaning as TrxSplit.curAmount, so is negative for expenses.
    Private mcurBudgetLimit As Decimal
    'First date in the budget period.
    Private mdatBudgetStarts As Date
    'Last date in the budget period.
    Private mdatBudgetEnds As Date
    'For budget Trx, a unique ID for that budget. All budget Trx in a repeating
    'budget will have the same mstrBudgetKey, and the appropriate one for applying
    'BaseTrx will be chosen by the budget period. Is a foreign key into some external
    'database defining how to create budget BaseTrx.
    Private mstrBudgetKey As String
    'Amount applied toward budget. Magnitude may be greater than mcurBudgetLimit,
    'in which case mcurAmount will equal zero instead of a credit.
    Private mcurBudgetApplied As Decimal
    'Budget is expired (amount will be zero).
    Private mblnIsExpired As Boolean
    'Collection of TrxSplit objects belonging to other BankTrx and applied to this BudgetTrx.
    Private mcolAppliedSplits As List(Of TrxSplit)

    Public Sub New(ByVal objReg_ As Register)
        MyBase.New(objReg_)
    End Sub

    '$Description Initialize a new budget BaseTrx object. Normally followed by
    '   Register.NewLoadEnd() or Register.NewAddEnd().

    Public Sub NewStartBudget(ByVal blnWillAddToRegister As Boolean, ByVal datDate_ As Date, ByVal strDescription_ As String,
                              ByVal strMemo_ As String, ByVal blnAwaitingReview_ As Boolean, ByVal blnAutoGenerated_ As Boolean,
                              ByVal intRepeatSeq_ As Integer, ByVal strRepeatKey_ As String, ByVal curBudgetLimit_ As Decimal,
                              ByVal datBudgetStarts_ As Date, ByVal strBudgetKey_ As String)

        If blnWillAddToRegister Then
            RegisterInternal.ClearFirstAffected()
        End If

        NumberInternal = "Budget"
        TrxDateInternal = datDate_
        DescriptionInternal = strDescription_
        MemoInternal = strMemo_
        StatusInternal = TrxStatus.NonBank
        IsFakeInternal = True
        IsAwaitingReviewInternal = blnAwaitingReview_
        IsAutoGeneratedInternal = blnAutoGenerated_
        RepeatSeqInternal = intRepeatSeq_
        RepeatKeyInternal = strRepeatKey_

        mcurBudgetLimit = curBudgetLimit_
        mdatBudgetStarts = datBudgetStarts_
        mdatBudgetEnds = datDate_
        PutPeriodDatesInCorrectOrder()
        mstrBudgetKey = strBudgetKey_
        mcurBudgetApplied = 0
        mblnIsExpired = False

        If blnWillAddToRegister Then
            SetAmountForBudget()
        End If
        BalanceInternal = 0

        mcolAppliedSplits = New List(Of TrxSplit)

        RaiseErrorOnBadData("NewStartBudget")
        RaiseErrorOnBadBudget("NewStartBudget")

    End Sub

    '$Description Update all updatable properties of this budget BaseTrx object.
    '   Normally followed Register.UpdateEnd().

    Public Sub UpdateStartBudget(ByVal datDate_ As Date, ByVal strDescription_ As String, ByVal strMemo_ As String,
                                 ByVal blnAwaitingReview_ As Boolean, ByVal blnAutoGenerated_ As Boolean,
                                 ByVal intRepeatSeq_ As Integer, ByVal strRepeatKey_ As String, ByVal curBudgetLimit_ As Decimal,
                                 ByVal datBudgetStarts_ As Date, ByVal strBudgetKey_ As String)

        TrxDateInternal = datDate_
        DescriptionInternal = strDescription_
        MemoInternal = strMemo_
        IsAwaitingReviewInternal = blnAwaitingReview_
        IsAutoGeneratedInternal = blnAutoGenerated_
        RepeatSeqInternal = intRepeatSeq_
        RepeatKeyInternal = strRepeatKey_
        mcurBudgetLimit = curBudgetLimit_
        mdatBudgetStarts = datBudgetStarts_
        mdatBudgetEnds = datDate_
        PutPeriodDatesInCorrectOrder()
        mstrBudgetKey = strBudgetKey_

        'We do NOT clear mcurBudgetApplied, because updating the budget
        'does not change what splits have been applied to it.
        SetAmountForBudget()
        BalanceInternal = 0

        'Will not unapply any budgets, even though the budget period or budget key
        'may have been changed by this update. The caller should warn the operator
        'that normal transactions will not be reapplied to budgets until they are
        'edited and saved or the register reloaded.

        RaiseErrorOnBadData("UpdateStartBudget")
        RaiseErrorOnBadBudget("UpdateStartBudget")
    End Sub

    ''' <summary>
    ''' Only needed to load old data files that have trx date
    ''' equal to the ending date, not the starting date.
    ''' This method can be removed after all old data files
    ''' have been loaded and saved. Which might be hard to 
    ''' determine, because a .ACT file will not be saved
    ''' until there is actually a change in it.
    ''' </summary>
    Private Sub PutPeriodDatesInCorrectOrder()
        If mdatBudgetStarts > mdatBudgetEnds Then
            Dim datTemp As Date = mdatBudgetStarts
            mdatBudgetStarts = mdatBudgetEnds
            mdatBudgetEnds = datTemp
            TrxDateInternal = datTemp
            Me.Register.Account.SetChanged()
        End If
    End Sub

    Public Property BudgetLimit() As Decimal
        Get
            Return mcurBudgetLimit
        End Get
        Set(value As Decimal)
            mcurBudgetLimit = value
        End Set
    End Property

    Public ReadOnly Property BudgetStarts() As Date
        Get
            Return mdatBudgetStarts
        End Get
    End Property

    Public ReadOnly Property BudgetEnds() As Date
        Get
            Return mdatBudgetEnds
        End Get
    End Property

    Public Function InBudgetPeriod(ByVal datTarget As DateTime) As Boolean
        Return datTarget >= mdatBudgetStarts And datTarget <= mdatBudgetEnds
    End Function

    Public ReadOnly Property BudgetKey() As String
        Get
            Return mstrBudgetKey
        End Get
    End Property

    Public ReadOnly Property BudgetApplied() As Decimal
        Get
            Return mcurBudgetApplied
        End Get
    End Property

    Public ReadOnly Property IsExpired() As Boolean
        Get
            Return mblnIsExpired
        End Get
    End Property

    Public Overrides ReadOnly Property CategoryLabel As String
        Get
            Return ""
        End Get
    End Property

    Protected Sub RaiseErrorOnBadBudget(ByVal strRoutine As String)
        If mstrBudgetKey = "" Then
            RaiseErrorMsg("Missing budget key in " & strRoutine)
        End If
        If mdatBudgetEnds = Utilities.EmptyDate Then
            RaiseErrorMsg("Missing budget end date in " & strRoutine)
        End If
        If mdatBudgetEnds < mdatBudgetStarts Then
            RaiseErrorMsg("Budget period ends before it begins")
        End If
    End Sub

    '$Description Set mcurAmount for a budget BaseTrx. Called whenever
    '   mcurBudgetApplied or mcurBudgetLimit changes.

    Public Sub SetAmountForBudget()
        If mdatBudgetEnds < RegisterInternal.OldestBudgetEndAllowed Or System.Math.Abs(mcurBudgetApplied) > System.Math.Abs(mcurBudgetLimit) Then
            AmountInternal = 0
            mblnIsExpired = True
        Else
            AmountInternal = mcurBudgetLimit - mcurBudgetApplied
            mblnIsExpired = False
        End If
    End Sub

    Public Overrides Sub AddApply()
        ApplySplits(False)
    End Sub

    Public Overrides Sub UpdateUnApply()
        UnApplySplits()
    End Sub

    Public Overrides Sub UpdateApply()
        ApplySplits(False)
    End Sub

    Public Overrides Sub DeleteUnApply()
        UnApplySplits()
    End Sub

    Public Overrides Sub LoadApply()

    End Sub

    Public Overrides Sub LoadFinish()
        ApplySplits(True)
        SetAmountForBudget()
    End Sub

    Private Sub UnApplySplits()
        For Each objSplit As TrxSplit In mcolAppliedSplits
            objSplit.Budget = Nothing
        Next objSplit
        mcolAppliedSplits = New List(Of TrxSplit)()
        mcurBudgetApplied = 0D
        SetAmountForBudget()
        RegisterInternal.FireBudgetChanged(Me)
    End Sub

    Private Sub ApplySplits(ByVal blnLoading As Boolean)
        Dim blnAnyApplied As Boolean = False
        For Each objScanTrx As BaseTrx In New RegForwardFrom(Of BaseTrx)(RegisterInternal, EarliestPossibleApplied())
            If objScanTrx.TrxDate > mdatBudgetEnds Then
                Exit For
            End If
            Dim objNormalTrx As BankTrx = TryCast(objScanTrx, BankTrx)
            If Not objNormalTrx Is Nothing Then
                For Each objSplit As TrxSplit In objNormalTrx.Splits
                    If objSplit.Budget Is Nothing Then
                        If objSplit.BudgetKey = Me.BudgetKey Then
                            mcolAppliedSplits.Add(objSplit)
                            objSplit.Budget = Me
                            mcurBudgetApplied = mcurBudgetApplied + objSplit.Amount
                            blnAnyApplied = True
                        End If
                    End If
                Next
            End If
        Next
        If blnAnyApplied Then
            SetAmountForBudget()
            RegisterInternal.FireBudgetChanged(Me)
        End If
    End Sub

    ''' <summary>
    ''' Return the earliest BaseTrx in this register that could possibly
    ''' be applied to this BudgetTrx. The returned object will always be non-nothing,
    ''' but it may not be a BankTrx and it may not be applied to the Budget.
    ''' It is merely the earliest that COULD be applied, based on the dates.
    ''' The caller normally scans forward from here until they find a BaseTrx
    ''' dated after mdatBudgetEnds.
    ''' </summary>
    ''' <returns></returns>
    Public Function EarliestPossibleApplied() As BaseTrx
        For Each objOtherTrx As BaseTrx In New RegBackwardFrom(Of BaseTrx)(RegisterInternal, Me)
            If objOtherTrx.TrxDate < mdatBudgetStarts Then
                Return objOtherTrx.NextTrx
            End If
        Next
        Return RegisterInternal.FirstTrx
    End Function

    '$Description Apply a Split to this BudgetTrx.

    Public Sub ApplyToThisBudget(ByVal objSplit As TrxSplit)
        mcolAppliedSplits.Add(objSplit)
        objSplit.Budget = Me
        mcurBudgetApplied = mcurBudgetApplied + objSplit.Amount
        SetAmountForBudget()
        RegisterInternal.FireBudgetChanged(Me)
    End Sub

    '$Description Un-apply a Split from this BudgetTrx.

    Public Sub UnApplyFromThisBudget(ByVal objSplit As TrxSplit)
        If Not mcolAppliedSplits.Remove(objSplit) Then
            RaiseErrorMsg("Could not find split in Trx.UnApplyFromThisBudget")
        End If
        mcurBudgetApplied = mcurBudgetApplied - objSplit.Amount
        SetAmountForBudget()
        RegisterInternal.FireBudgetChanged(Me)
    End Sub

    Public ReadOnly Property AppliedSplits As List(Of TrxSplit)
        Get
            Return mcolAppliedSplits
        End Get
    End Property

    Public Overrides Function CloneTrx(ByVal blnWillAddToRegister As Boolean) As BaseTrx
        Dim objBudgetTrx As BudgetTrx = New BudgetTrx(RegisterInternal)
        objBudgetTrx.NewStartBudget(blnWillAddToRegister, TrxDateInternal, DescriptionInternal, MemoInternal, IsAwaitingReviewInternal, IsAutoGeneratedInternal,
                                    RepeatSeqInternal, RepeatKeyInternal, mcurBudgetLimit, mdatBudgetStarts, mstrBudgetKey)
        objBudgetTrx.Amount = AmountInternal
        Return objBudgetTrx
    End Function

    Public Overrides Sub Validate()
        Dim objSplit As TrxSplit
        Dim curTotal As Decimal
        MyBase.Validate()
        If mstrBudgetKey = "" Then
            Register.FireValidationError(Me, "Budget trx requires budget key")
            Exit Sub
        End If
        If mcolAppliedSplits Is Nothing Then
            Register.FireValidationError(Me, "Missing applied split collection")
        Else
            curTotal = 0
            For Each objSplit In mcolAppliedSplits
                curTotal = curTotal + objSplit.Amount
                If Not objSplit.Budget Is Me Then
                    Register.FireValidationError(Me, "Split applied to budget trx has wrong objBudget")
                End If
                If objSplit.BudgetKey <> mstrBudgetKey Then
                    Register.FireValidationError(Me, "Split applied to budget trx has wrong budget key")
                End If
            Next objSplit
            If curTotal <> mcurBudgetApplied Then
                Register.FireValidationError(Me, "Budget trx applied splits add up wrong")
            End If
        End If
        If Not IsFakeInternal Then
            Register.FireValidationError(Me, "Budget trx must be fake")
        End If
    End Sub

    Public Overrides Function GetSummary() As String
        Return Me.TrxDate.ToShortDateString() + " " + Me.Description + " " + Me.BudgetLimit.ToString()
    End Function

    Public Overrides ReadOnly Property TrxTypeSortKey As Integer
        Get
            Return 3
        End Get
    End Property

    Public Overrides ReadOnly Property DocNumberSortKey As String
        Get
            Return ""
        End Get
    End Property

    Public Overrides ReadOnly Property AmountSortKey As Integer
        Get
            'Have to use mcurBudgetLimit instead of mcurAmount because the algorithms that
            'insert, update and delete transactions in the register require that only
            'the transaction being inserted, updated or deleted can have its register
            'position changed by that operation. BudgetTrx.mcurAmount can change when
            'a split is applied to it, which could cause this rule to be violated if
            'mcurAmount is used by this property.
            'So we use mcurBudgetLimit instead, which is never changes except when
            'inserting, updating or deleting the budget.

            Return If(mcurBudgetLimit > 0, 0, 1)
            'Return MyBase.intAmountSortKey()
        End Get
    End Property
End Class
